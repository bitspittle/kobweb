plugins {
    kotlin("multiplatform")
    id("org.jetbrains.compose")
    id("com.varabyte.kobweb.internal.publish")
}

group = "com.varabyte.kobweb"
version = libs.versions.kobweb.libs.get()

kotlin {
    js(IR) {
        browser()
        binaries.executable()
    }
    sourceSets {
        val jsMain by getting {
            dependencies {
                implementation(compose.web.core)
                implementation(compose.runtime)

                api(project(":frontend:kobweb-compose"))
            }
        }
    }
}

kobwebPublication {
    artifactId.set("kobweb-silk-icons-mdi")
    description.set("A collection of Kobweb Silk components that directly wrap Material Design icons")
}

enum class IconCategory {
    OUTLINED,
    ROUND,
    SHARP,
    TWO_TONE;
}

val regenerateIconsTask = tasks.register("regenerateIcons") {
    val srcFile = layout.projectDirectory.file("md-icon-list.txt")
    val dstFile =
        layout.projectDirectory.file("src/jsMain/kotlin/com/varabyte/kobweb/silk/components/icons/mdi/MdIcons.kt")

    inputs.files(srcFile, layout.projectDirectory.file("build.gradle.kts"))
    outputs.file(dstFile)

    // {SOLID=[ad, address-book, address-card, ...], REGULAR=[address-book, address-card, angry, ...], ... }
    val iconRawNames = srcFile.asFile
        .readLines().asSequence()
        .filter { line -> !line.startsWith("#") }
        .map { line ->
            line.split("=", limit = 2).let { parts ->
                if (parts[0] != "mdi") {
                    throw GradleException("Unexpected category string: ${parts[0]}")
                }
                val names = parts[1]
                names.split(",")
            }
        }
        .toSet()

    // For each icon name, figure out what categories they are in. This will affect the function signature we generate.
    // {ad=[SOLID], address-book=[SOLID, REGULAR], address-card=[SOLID, REGULAR], ...
    val iconCategories = mutableMapOf<String, Set<IconCategory>>()
    iconRawNames.forEach { entry ->
        entry.forEach { rawName ->
            iconCategories.computeIfAbsent(rawName) {
                setOf(
                    IconCategory.OUTLINED,
                    IconCategory.ROUND,
                    IconCategory.SHARP,
                    IconCategory.TWO_TONE,
                )
            }
        }
    }

    // Sanity check results
    iconCategories
        .filterNot { entry ->
            val categories = entry.value
            categories.size == 1 ||
                (categories.size == 4 && categories.contains(IconCategory.OUTLINED) && categories.contains(IconCategory.ROUND) && categories.contains(IconCategory.SHARP) && categories.contains(IconCategory.TWO_TONE))
        }
        .let { invalidGroupings ->
            if (invalidGroupings.isNotEmpty()) {
                throw GradleException("Found unexpected groupings. An icon should only be in its own category OR it can have solid and regular versions: $invalidGroupings")
            }
        }

    // Generate four types of functions: solid only, regular only, solid or regular, and brand
    val iconMethodEntries = iconCategories
        .map { entry ->
            // TODO: Figure out how to get ligatures to work
            val (rawName, ligature) = entry.key.split(" ", limit = 2)
            val methodName = rawName.split("_").joinToString("") { it.capitalize() }

            "@Composable fun Mdi$methodName(modifier: Modifier = Modifier, status: IconStatus = IconStatus.ACTIVE, mode: IconMode = IconMode.LIGHT, style: IconCategory = IconCategory.OUTLINED) = MdiIcon(\"$rawName\", modifier, status, mode, style)"
        }

    val iconsCode = """
@file:Suppress("unused", "SpellCheckingInspection")

// !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
// THIS FILE IS AUTOGENERATED.
//
// Do not edit this file by hand. Instead, update `md-icon-list.txt` in the module root and run the Gradle
// task "regenerateIcons"
// !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

package com.varabyte.kobweb.silk.components.icons.mdi

import androidx.compose.runtime.*
import com.varabyte.kobweb.compose.ui.Modifier
import com.varabyte.kobweb.compose.ui.toAttrs
import org.jetbrains.compose.web.dom.Span
import org.jetbrains.compose.web.dom.Text

enum class IconCategory(internal val className: String) {
    OUTLINED("-outlined"),
    ROUND("-round"),
    SHARP("-sharp"),
    TWO_TONE("-two-tone");
}

enum class IconStyle(internal val category: IconCategory) {
    OUTLINED(IconCategory.OUTLINED),
    ROUND(IconCategory.ROUND),
    SHARP(IconCategory.SHARP),
    TWO_TONE(IconCategory.TWO_TONE);
}

enum class IconStatus(internal val status: String) {
    ACTIVE(""),
    DISABLED("md-inactive");
}

enum class IconMode(internal val mode: String) {
    LIGHT("md-dark"),
    DARK("md-light");
}

@Composable
fun MdiIcon(
    name: String,
    modifier: Modifier,
    status: IconStatus = IconStatus.ACTIVE,
    mode: IconMode = IconMode.LIGHT,
    style: IconCategory = IconCategory.OUTLINED,
) {
    Span(
        attrs = modifier.toAttrs {
            classes(
                listOf(
                    "material-icons${'$'}{style.className}",
                    status.status,
                    mode.mode,
                )
            )
        }
    ) {
        Text(name)
      }
}

${iconMethodEntries.joinToString("\n")}
    """.trimIndent()

    println(dstFile.asFile.writeText(iconsCode))
}

tasks.named("compileKotlinJs") {
    dependsOn(regenerateIconsTask)
}

tasks.named("sourcesJar") {
    dependsOn(regenerateIconsTask)
}

tasks.named("jsSourcesJar") {
    dependsOn(regenerateIconsTask)
}